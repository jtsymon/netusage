#!/bin/perl

use strict;
use warnings;
use LWP::UserAgent;
use HTTP::Request::Common;
use JSON;

# Configure these variables
my $username    = 'EDIT ME';
my $password    = 'EDIT ME';
my $phoneNumber = 'EDIT ME';

#####################################################
# Shouldn't need to touch anything below this point #
#####################################################

my $curl = LWP::UserAgent->new;
$curl->cookie_jar( {} );

# login
$curl->request(POST 'https://www.spark.co.nz/portal/site/digital-site/template.PAGE/action.process/menuitem.354c3e0302b1aa1994910bf3bc407ea0/?javax.portlet.action=true&javax.portlet.tpst=b85a693813b5760362a127b4bc407ea0',
    Content => [
        username => $username,
        password => $password
    ]);

# fetch data
my $response = $curl->request(POST 'https://www.spark.co.nz/portal/site/mytelecom-site/template.BINARYPORTLET/menuitem.c26e41f6aeaa53d794910bf3bc407ea0/resource.process/?javax.portlet.tpst=9252c8781852ea6e85cdc707d5107ea0&javax.portlet.rid_9252c8781852ea6e85cdc707d5107ea0=ajaxGetDataUsage',
    Content => [
        phoneNumber => $phoneNumber
    ]);

# logout
$curl->request(GET 'https://www.spark.co.nz/portal/site/mytelecom-site/template.PAGE/action.process/menuitem.c26e41f6aeaa53d794910bf3bc407ea0/?javax.portlet.action=true&javax.portlet.tpst=01921f5d7965760362a127b4bc407ea0');

my $json = decode_json($response->decoded_content)->{'actionResult'};

# timestamps are in milliseconds and need to be converted to seconds
my $startTime = $json->{'summaryFrom'} * 0.001;
my $endTime = $json->{'summaryTo'} * 0.001;
my $currentTime = time;
my $elapsedTime = $currentTime - $startTime;
my $remainingTime = $endTime - $currentTime;
my $totalTime = $endTime - $startTime;
my $percentTime = ($remainingTime) / ($elapsedTime);

# data is in bytes and needs to be converted to mebibytes
my $used = $json->{'totalUsage'} / 1048576;
my $allowed = $json->{'monthlyDataAllowance'} / 1048576;
my $remaining = $allowed - $used;
my $percentUsed = ($used / $allowed) * 100;

printf "Used %0.2f MiB (%0.2f%%), %0.0f/%0.0f MiB remaining\n", (
    $used,
    $percentUsed,
    $remaining,
    $allowed
);
printf "%02d days, %02d:%02d since reset, %02d days, %02d:%02d remaining\n", (
    int($elapsedTime / 86400),
    int(($elapsedTime % 86400) / 3600),
    int(($elapsedTime % 3600) / 60),
    int($remainingTime / 86400),
    int(($remainingTime % 86400) / 3600),
    int(($remainingTime % 3600) / 60)
);
printf "Used %0.2f MiB per day, %0.2f MiB per day remaining\n", (
    $used * 86400 / $elapsedTime,
    $remaining * 86400 / $remainingTime
);
